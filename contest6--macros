DATA gun (DROP = n seed);      /*we don't need these two vars, so drop*/
DO n=1 TO 10;
   RETAIN seed 45;
   x1=RANNOR(seed)*1-0.1;
   x2=RANNOR(seed)*1+0.1;
   x3=RANNOR(seed)*1-0.2;
   x4=RANNOR(seed)*1+0.2;
   x5=RANNOR(seed)*1-0.3;
   x6=RANNOR(seed)*1+0.3;
   x7=RANNOR(seed)*1-0.4;
   x8=RANNOR(seed)*1+0.4;
   x9=RANNOR(seed)*1-0.5;
   x10=RANNOR(seed)*1+0.5;
   x11=RANNOR(seed)*1-0.6;
   x12=RANNOR(seed)*1+0.6;
   OUTPUT;
END;
RUN;

/*put statistics in a new SAS data file, one variable at a time*/
/*define a macro to do this*/
%MACRO FINCH(vrbl);
PROC MEANS DATA=gun NOPRINT;
   VAR &vrbl;
   OUTPUT OUT=tmp N=N
                                 MIN=MIN
                                 MAX=MAX
                                 MEAN=MEAN
                                 STD=STD
                                 MEDIAN=MEDIAN
                                 RANGE=RANGE;
RUN;
DATA &vrbl;
   SET tmp;
   LENGTH _NAME_ $4;
   CALL VNAME(&vrbl,_NAME_);
   LABEL N='N'
             MIN='MIN'
             MAX='MAX'
             MEAN='MEAN'
             STD='STD'
             MEDIAN='MEDIAN'
             RANGE='RANGE'
			 _NAME_='VARIABLE';
RUN;
DATA &vrbl (DROP=_TYPE_ _FREQ_ N &vrbl);
   RETAIN _NAME_;
   SET &vrbl;
RUN;
QUIT;
%MEND FINCH;

%MACRO STEVE(all_vars);
  %LET k=1;
  %LET var = %SCAN(&all_vars, &k);
  %DO %WHILE("&var" ~= "");
         %FINCH(&var);   
         %LET k = %EVAL(&k + 1);
         %LET var = %SCAN(&all_vars, &k);
  %END; 
%MEND STEVE;

/* listing all variables in a SAS dataset */
/* one way: the SHORT OPTION of PROC CONTENTS*/

PROC CONTENTS DATA=gun VARNUM SHORT;
RUN;

/* could then copy & paste string as argument for STEVE */
/* another way uses macros */
/* http://support.sas.com/ctx/samples/index.jsp?sid=597 */

%MACRO LST(dsn);
  %GLOBAL x;
  %LET x=;
  %LET dsid=%SYSFUNC(open(&dsn));   /*open dataset*/
  %LET cnt=%SYSFUNC(attrn(&dsid,nvars));  /*count vars*/
  %DO i = 1 %TO &cnt; /* loop from 1 to cnt*/
     %LET x=&x %SYSFUNC(varname(&dsid,&i));   /*x contains all var names*/
  %END;
  %LET rc=%SYSFUNC(close(&dsid));   /*close dataset*/
%MEND LST;

%LST(gun);   /*apply macro to dataset*/
%PUT;
%PUT &x;     /*written to log window, not to output window*/

%STEVE(&x);   /*this creates twelve files like before*/

/*concatenate the twelve files*/
DATA gun1;
   SET &x;
   LABEL _NAME_='VARIABLE';
RUN;
/*no need to switch around rows and columns: skip transposition*/

/*export this file to Excel (managers love Excel!)*/
PROC EXPORT DATA= WORK.GUN1 
            OUTFILE= "C:\Users\sfinch\Desktop\TEMP\gun_2.xls" 
            DBMS=EXCELCS LABEL REPLACE;
     SHEET="fun"; 
RUN;




