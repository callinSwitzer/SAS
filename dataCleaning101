*-----------------------------------*
| PROGRAM NAME: PATIENTS.SAS |
| PURPOSE: TO CREATE A SAS DATA SET |
| CALLED PATIENTS |
| DATE: DECEMBER 17, 1998 |
*-----------------------------------*;
DATA PATIENTS;
INFILE "C:\Users\cswitzer\Desktop\PATIENTS.TXT" PAD;
INPUT @1 PATNO $3.
@4 GENDER $1.
@5 VISIT MMDDYY10.
@15 HR 3.
@18 SBP 3.
@21 DBP 3.
@24 DX $3.
@27 AE $1.;
LABEL PATNO = "PATIENT NUMBER"
GENDER = "GENDER"
VISIT = "VISIT DATE"
HR = "HEART RATE"
SBP = "SYSTOLIC BLOOD PRESSURE"
DBP = "DIASTOLIC BLOODPRESSURE"
DX = "DIAGNOSIS CODE"
AE = "ADVERSE EVENT?";
FORMAT VISIT MMDDYY10.;
RUN;
proc print data=patients;
run;
proc contents data=patients varnum;
run;
PROC FREQ DATA=PATIENTS;
TITLE "FREQUENCY COUNTS";
TABLES GENDER DX AE / NOCUM
NOPERCENT;
RUN;
DATA _NULL_;
INFILE "C:\Users\cswitzer\Desktop\PATIENTS.TXT" PAD;
FILE PRINT; ***SEND OUTPUT TO THE
OUTPUT WINDOW;
TITLE "LISTING OF INVALID DATA";
***NOTE: WE WILL ONLY INPUT THOSE
VARIABLES OF INTEREST;
INPUT @1 PATNO $3.
@4 GENDER $1.
@24 DX $3.
@27 AE $1.;
***CHECK GENDER;
IF GENDER NOT IN ('F','M',' ') THEN
PUT PATNO= GENDER=;
***CHECK DX;
IF VERIFY(DX,' 0123456789') NE 0
THEN PUT PATNO= DX=;
***CHECK AE;
IF AE NOT IN ('0','1',' ') THEN PUT
PATNO= AE=;
RUN;
PROC PRINT DATA=PATIENTS;
WHERE HR NOT BETWEEN 40 AND 100 AND
HR IS NOT MISSING OR
SBP NOT BETWEEN 80 AND 200 AND
SBP IS NOT MISSING OR
DBP NOT BETWEEN 60 AND 120 AND
DBP IS NOT MISSING;
TITLE "OUT-OF-RANGE VALUES FOR NUMERIC
VARIABLES";
ID PATNO;
VAR HR SBP DBP;
RUN;
DATA _NULL_;
INFILE "C:\Users\cswitzer\Desktop\PATIENTS.TXT" PAD;
FILE PRINT; ***OUTPUT TO THE OUTPUT WINDOW;
TITLE "LISTING OF PATIENT NUMBERS AND INVALID
DATA VALUES";
***NOTE: WE WILL ONLY INPUT THOSE VARIABLES OF
INTEREST;
INPUT @1 PATNO $3.
@15 HR 3.
@18 SBP 3.
@21 DBP 3.;
***CHECK HR;
IF (HR LT 40 AND HR NE .) OR HR GT 100 THEN
PUT PATNO= HR=;
***CHECK SBP;
IF (SBP LT 80 AND SBP NE .) OR SBP GT 200 THEN
PUT PATNO= SBP=;
***CHECK DBP;
IF (DBP LT 60 AND DBP NE .) OR DBP GT 120 THEN
PUT PATNO= DBP=;
RUN;
***USING PROC MEANS TO DETECT INVALID
VALUES AND MISSING VALUES IN THE
PATIENTS DATA SET;
PROC MEANS DATA=PATIENTS N NMISS
MIN MAX MAXDEC=0;
TITLE "CHECKING NUMERIC VARIABLES";
VAR HR SBP DBP;
RUN;
PROC UNIVARIATE DATA=PATIENTS PLOT;
TITLE "USING PROC UNIVARIATE";
VAR HR SBP DBP;
RUN;
PROC UNIVARIATE DATA=PATIENTS PLOT;
TITLE "USING PROC UNIVARIATE";
ID PATNO;
VAR HR SBP DBP;
RUN;
DATA _NULL_;
INFILE "C:\Users\sfinch\Desktop\PATIENTS.TXT" PAD;
FILE PRINT; ***SEND OUTPUT TO THE
OUTPUT WINDOW;
TITLE "LISTING OF INVALID DATA VALUES";
***NOTE: WE WILL ONLY INPUT THOSE
VARIABLES OF INTEREST;
INPUT @1 PATNO $3.
@15 HR 3.
@18 SBP 3.
@21 DBP 3.;
***CHECK HR;
IF (HR LT 40 AND HR NE .) OR HR GT 100
THEN PUT PATNO= HR=;
***CHECK SBP;
IF (SBP LT 80 AND SBP NE .) OR SBP GT
200 THEN PUT PATNO= SBP=;
***CHECK DBP;
IF (DBP LT 60 AND DBP NE .) OR DBP GT
120 THEN PUT PATNO= DBP=;
RUN;
***PROGRAM TO CHECK FOR OUT OF RANGE
VALUES, USING USER DEFINED FORMATS;
PROC FORMAT;
VALUE HR_CK 40-100, . = 'OK';
VALUE SBP_CK 80-200, . = 'OK';
VALUE DBP_CK 60-120, . = 'OK';
RUN;
DATA _NULL_;
INFILE "C:\Users\sfinch\Desktop\PATIENTS.TXT" PAD;
FILE PRINT; ***SEND OUTPUT TO THE
OUTPUT WINDOW;
TITLE "LISTING OF INVALID VALUES";
***NOTE: WE WILL ONLY INPUT THOSE
VARIABLES OF INTEREST;
INPUT @1 PATNO $3.
@15 HR 3.
@18 SBP 3.
@21 DBP 3.;
IF PUT(HR,HR_CK.) NE 'OK' THEN PUT
PATNO= HR=;
IF PUT(SBP,SBP_CK.) NE 'OK' THEN PUT
PATNO= SBP=;
IF PUT(DBP,DBP_CK.) NE 'OK' THEN PUT
PATNO= DBP=;
RUN;
***SOLUTION USING PROC UNIVARIATE AND
PERCENTILES;
***THE TWO MACRO VARIABLES THAT FOLLOW
DEFINE THE LOWER AND UPPER PERCENTILE
CUT POINTS;
%LET LOW_PER=20; 
%LET UP_PER=80; 
***CHOOSE A VARIABLE TO OPERATE ON;
%LET VAR = HR; 
PROC UNIVARIATE DATA=PATIENTS NOPRINT;
VAR &VAR;
ID PATNO;
OUTPUT OUT=TMP PCTLPTS=&LOW_PER &UP_PER
PCTLPRE = L_; 
RUN;
DATA HILO;
SET PATIENTS(KEEP=PATNO &VAR); 
***BRING IN LOWER AND UPPER CUTOFFS
FOR VARIABLE;
IF _N_ = 1 THEN SET TMP; 
IF &VAR LE L_&LOW_PER THEN DO;
RANGE = 'LOW ';
OUTPUT;
END;
ELSE IF &VAR GE L_&UP_PER THEN DO;
RANGE = 'HIGH';
OUTPUT;
END;
RUN;
PROC SORT DATA=HILO(WHERE=(&VAR NE .));
BY DESCENDING RANGE &VAR; 
RUN;
PROC PRINT DATA=HILO;
TITLE "LOW AND HIGH VALUES";
ID PATNO;
VAR RANGE &VAR;
RUN;
***ASSIGN VALUES TO TWO MACRO VARIABLES;
%LET VAR = HR;
%LET IDVAR = PATNO;
PROC SORT DATA=PATIENTS(KEEP=&IDVAR
&VAR 
WHERE=(&VAR NE .))
OUT=TMP;
BY &VAR;
RUN;
DATA _NULL_;
TITLE "TEN LOWEST AND TEN HIGHEST VALUES FOR
VAR";
SET TMP NOBS=NUM_OBS; 
HIGH = NUM_OBS - 9; 
FILE PRINT;
IF _N_ LE 10 THEN DO; 
IF _N_ = 1 THEN PUT / "TEN LOWEST VALUES" ;
PUT "&ID = " &IDVAR @15 &VAR;
END;
IF _N_ GE HIGH THEN DO; 
IF _N_ = HIGH THEN PUT / "TEN HIGHEST
VALUES";
PUT "&ID = " &IDVAR @15 &VAR;
END;
RUN;
PROC MEANS DATA=PATIENTS NOPRINT;
VAR HR SBP DBP;
OUTPUT OUT=MEANS(DROP=_TYPE_ _FREQ_)
MEAN=M_HR M_SBP M_DBP
STD=S_HR S_SBP S_DBP;
RUN;
DATA _NULL_;
FILE PRINT;
%LET N_SD = 2;
SET PATIENTS;
IF _N_ = 1 THEN SET MEANS;
ARRAY RAW[3] HR SBP DBP;
ARRAY MEAN[3] M_HR M_SBP M_DBP;
ARRAY STD[3] S_HR S_SBP S_DBP;
DO I = 1 TO 3;
IF RAW[I] LT MEAN[I] - &N_SD*STD[I] AND
RAW[I] NE .
OR RAW[I] GT MEAN[I] + &N_SD*STD[I] THEN
PUT PATNO= RAW[I]=;
END;
RUN;
PROC RANK DATA=PATIENTS OUT=TMP GROUPS=4;
VAR HR;
RANKS R_HR;
RUN;
PROC MEANS DATA=TMP NOPRINT;
WHERE R_HR IN (1,2); ***THE MIDDLE 50%;
VAR HR;
OUTPUT OUT=MEANS(DROP=_TYPE_ _FREQ_)
MEAN=M_HR STD=S_HR;
RUN;
DATA _NULL_;
TITLE "OUTLIERS BASED ON TRIMMED SD";
FILE PRINT;
%LET N_SD = 5.25;
SET PATIENTS;
IF _N_ = 1 THEN SET MEANS;
IF HR LT M_HR - &N_SD*S_HR AND HR NE .
OR HR GT M_HR + &N_SD*S_HR THEN PUT PATNO=
HR=;
RUN;
